apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitBranch = (System.getenv("CIRCLE_BRANCH") != null) ? System.getenv("CIRCLE_BRANCH") :
    'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("JST"))

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    defaultConfig {
        applicationId "io.github.yamacraft.app.sampra"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}-${gitSha}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "GIT_BRANCH", "\"${gitBranch}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        buildConfigField "String", "TWITCH_CLIENT_ID", "\"${properties.getProperty("TWITCH_CLIENT_ID")}\""
        buildConfigField "String", "FUNCTIONS_BASE_URL", "\"${properties.getProperty("FUNCTIONS_BASE_URL")}\""
        buildConfigField "String", "GOOGLE_AUTH_WEB_CLIENT_ID", "\"${properties.getProperty("GOOGLE_AUTH_WEB_CLIENT_ID")}\""
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            debuggable true
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "com.google.firebase:firebase-core:17.2.0"
    implementation "com.google.firebase:firebase-auth:19.0.0"
    implementation "com.google.firebase:firebase-firestore:21.1.1"

    implementation "com.google.android.gms:play-services-auth:17.0.0"

    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.core:core-ktx:1.1.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.1.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.browser:browser:1.0.0"

    implementation "com.squareup.okhttp3:okhttp:3.12.1"
    implementation "com.squareup.okhttp3:logging-interceptor:3.12.1"
    implementation "com.squareup.retrofit2:retrofit:2.6.1"
    implementation "com.squareup.retrofit2:converter-gson:2.6.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.2"

    implementation "com.jakewharton.timber:timber:4.7.1"

    testImplementation "junit:junit:4.12"

    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
}
apply plugin: 'com.google.gms.google-services'
